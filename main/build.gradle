apply plugin: 'android'
//apply plugin: 'hugo'
apply plugin: 'eclipse'
apply plugin: 'robolectric'



android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
    flavorDimensions "stores", "apis"
    lintOptions {
        disable 'MissingTranslation'
    }
    defaultConfig {
        testApplicationId "de.azapps.mirakelandroid.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        minSdkVersion 15
        targetSdkVersion 19
    }
    signingConfigs {
        release {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFile 'proguard-android.txt'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        google {
            flavorDimension "stores"
        }
        fdroid {
            flavorDimension "stores"
        }
        l_test {
            flavorDimension "apis"
            minSdkVersion "L"
            targetSdkVersion "L"
            compileSdkVersion "android-L"
        }
        normal {
            flavorDimension "apis"
            compileSdkVersion rootProject.ext.compileSdkVersion
        }
        old {
            flavorDimension "apis"
            minSdkVersion 8
            targetSdkVersion 19
            compileSdkVersion rootProject.ext.compileSdkVersion
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest.setRoot('tests')
        androidTest {
            manifest.srcFile 'AndroidManifest.xml'
            java { srcDirs = ['tests/src'] }
            res.srcDirs = ['tests/res']
            assets.srcDirs = ['tests/assets']
            resources.srcDirs = ['tests/src']
        }

        old { java.srcDirs = ['src-old'] }

        google {
            manifest.srcFile  "res-google/AndroidManifest.xml"
            res.srcDirs = ['res-google']
        }
    }
}


dependencies {
    compile project(':model')
    compile project(':helper')
    compile project(':widget')
    compile project(':tw_sync')
    compile project(':changelog')
    compile project(':settings')
    compile project(':ilovefs-android')
    compile project(':custom_views')
    compile project(':acra')
    compile project(':drag-sort-listview')
    l_testCompile project(path: ':new_ui', configuration: 'l_testRelease')
    normalCompile project(path: ':new_ui', configuration: 'normalRelease')

    //for tests
    androidTestCompile files("$project.buildDir/classes/debug")
    androidTestCompile "com.jayway.android.robotium:robotium-solo:$robotiumVersion"
    androidTestCompile "junit:junit:$junitVersion"
}


